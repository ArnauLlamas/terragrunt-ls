generate
---
The `generate` block can be used to arbitrarily `generate` a file in the terragrunt working directory (where terraform is called). This can be used to `generate` common terraform configurations that are shared across multiple terraform modules. For example, you can use `generate` to `generate` the provider blocks in a consistent fashion by defining a `generate` block in the parent terragrunt config.

The `generate` block supports the following arguments:

* `name` (label): You can define multiple `generate` blocks in a single terragrunt config. As such, each block needs a name to differentiate between the other blocks.
* `path` (attribute): The path where the generated file should be written. If a relative path, itâ€™ll be relative to the Terragrunt working dir (where the terraform code lives).
* `if_exists` (attribute): What to do if a file already exists at `path`. Valid values are: `overwrite` (overwrite the existing file), `overwrite_terragrunt` (overwrite the existing file if it was generated by terragrunt; otherwise, error) `skip` (skip code generation and leave the existing file as-is), `error` (exit with an error).
* `comment_prefix` (attribute): A prefix that can be used to indicate comments in the generated file. This is used by terragrunt to write out a signature for knowing which files were generated by terragrunt. Defaults to # . Optional.
* `disable_signature` (attribute): When `true`, disables including a signature in the generated file. This means that there will be no difference between `overwrite_terragrunt` and `overwrite` for the `if_exists` setting. Defaults to `false`. Optional.
* `contents` (attribute): The contents of the generated file.
* `disable` (attribute): Disables this generate block.

Example:

```hcl
# When using this terragrunt config, terragrunt will `generate` the file "provider.tf" with the aws provider block before
# calling to terraform. Note that this will overwrite the `provider.tf` file if it already exists.
generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite"
  contents = <<EOF
provider "aws" {
  region              = "us-east-1"
  version             = "= 2.3.1"
  allowed_account_ids = ["1234567890"]
}
EOF
}
```

Note that `generate` can also be set as an attribute. This is useful if you want to set `generate` dynamically. For example, if in `common.hcl` you had:
```hcl
  generate "provider" {
    path      = "provider.tf"
    if_exists = "overwrite"
    contents = <<EOF
provider "aws" {
  region              = "us-east-1"
  version             = "= 2.3.1"
  allowed_account_ids = ["1234567890"]
}
EOF
}
```

Then in a `terragrunt.hcl` file, you could dynamically set `generate` as an attribute as follows:

```hcl
locals {
  # Load the data from common.hcl
  common = read_terragrunt_config(find_in_parent_folders("common.hcl"))
}

# Set the `generate` config dynamically to the `generate` config in common.hcl
generate = local.common.`generate`
```
